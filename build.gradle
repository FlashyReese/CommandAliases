plugins {
	id 'fabric-loom' version '1.2-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.10.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}+mc${project.minecraft_version}-${getVersionMetadata()}"
group = project.maven_group

repositories {
	maven {
		name = "Lucko"
		url 'https://oss.sonatype.org/content/repositories/snapshots'
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url "https://maven.nucleoid.xyz/"
		name "Nucleoid"
	}
	mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Permissions API
	include(modImplementation("me.lucko:fabric-permissions-api:0.2-SNAPSHOT"))

	// LevelDB
	include(implementation("org.iq80.leveldb:leveldb:0.12"))
	include(implementation("org.iq80.leveldb:leveldb-api:0.12"))

	// MySQL
	include(implementation("mysql:mysql-connector-java:8.0.33"))

	// Redis
	include(implementation("redis.clients:jedis:4.4.3"))
	include(implementation("org.apache.commons:commons-pool2:2.11.1"))

	// Parsing Libraries
	include(implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-toml:2.15.2"))
	include(implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.15.2"))
	include(implementation("org.yaml:snakeyaml:2.0"))
	include(implementation("com.fasterxml.jackson.core:jackson-core:2.15.2"))
	include(implementation("com.fasterxml.jackson.core:jackson-databind:2.15.2"))
	include(implementation("com.fasterxml.jackson.core:jackson-annotations:2.15.2"))

	// Math Evaluator
	include(implementation("com.fathzer:javaluator:3.0.3"))

	// Placeholders API
	modImplementation include("eu.pb4:placeholder-api:2.1.2+1.20.1")

	//modRuntimeOnly "maven.modrinth:lazydfu:0.1.3"
	//modRuntimeOnly "maven.modrinth:sodium:mc1.19-0.4.2"
	//modRuntimeOnly "maven.modrinth:lithium:mc1.19-0.8.0"
	//modRuntimeOnly "maven.modrinth:phosphor:mc1.19.x-0.8.1"
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE.txt")
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		maven {
			name = "FlashyReeseReleases"
			url = "https://maven.flashyreese.me/releases"
			credentials {
				username = System.getenv("MAVEN_USERNAME")
				password = System.getenv("MAVEN_PASSWORD")
			}
		}
		maven {
			name = "FlashyReeseSnapshots"
			url = "https://maven.flashyreese.me/snapshots"
			credentials {
				username = System.getenv("MAVEN_USERNAME")
				password = System.getenv("MAVEN_PASSWORD")
			}
		}
	}
}

static def getVersionMetadata() {
	def build_id = System.getenv("GITHUB_RUN_NUMBER")

	// CI builds only
	if (build_id != null) {
		return "build.${build_id}"
	}

	// No tracking information could be found about the build
	return "unknown"
}